apply plugin: 'signing'
apply from: "$rootDir/gradle/mavenRelease.gradle"
apply from: "$rootDir/gradle/bintrayRelease.gradle"

task release(dependsOn: [bintrayUpload, uploadArchives], description: 'Releases artifacts to public repositories')

def isRelease = {gradle.taskGraph.hasTask(release)}

javadoc.onlyIf isRelease
tasks.withType(Sign) {
    onlyIf isRelease
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    onlyIf isRelease
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
 
task sourcesJar(type: Jar) {
    onlyIf isRelease
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
	archives jar
	gradle.taskGraph.whenReady {
		if (isRelease()) {
			archives javadocJar
			archives sourcesJar
		}
	}
}

signing {
    sign configurations.archives
}
